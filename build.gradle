buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven { url = "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.wynprice.cursemaven:CurseMaven:1.2.3'
    }
}
apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'maven'

version = "1.17"
group = "teamroots.embers" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "EmbersRekindled"

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

jar {
    manifest {
        attributes 'FMLAT': 'embers_at.cfg'
    }
}

compileJava {
    sourceCompatibility = targetCompatibility = "1.8"
}

minecraft {
    version = "1.12.2-14.23.5.2826"
    runDir = "run"
    mappings = "snapshot_20171003"
    useDepAts = true
}

repositories {
    maven { url "http://maven.bluexin.be/repository/snapshots/" }
    maven { url 'http://dvs1.progwml6.com/files/maven' }
    maven { url "https://maven.blamejared.com/" }
    maven { url "http://www.ryanliptak.com/maven/" }
    maven { url "https://maven.thiakil.com" }
	maven { url "https://www.cursemaven.com" }
}

dependencies {
    compile "CraftTweaker2:CraftTweaker2-MC1120-Main:1.12-4.1.8.476"
    compile "mezz.jei:jei_1.12.2:4.16.1.302"
    compile "com.azanor.baubles:Baubles:1.12-1.5.2"
    deobfCompile "curse.maven:mysticalmechanics-300742:3006392"
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier "dev"
}

artifacts {
    archives deobfJar
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty("maven_pass")) {
                println "Uploading to remote maven."
                repository(url: "http://maven_upload.covers1624.net/") {
                    authentication(userName: "bordlistian", password: project.getProperty("maven_pass"))
                }
            }
            pom {
                groupId = project.group
                artifactId = project.archivesBaseName
            }
        }
    }
}

idea { module { inheritOutputDirs = true } }